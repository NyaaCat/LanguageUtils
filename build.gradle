apply plugin: "java-library"
apply plugin: "maven-publish"

ext.majorVersion = 2
ext.minorVersion = 5
ext.minecraftVersion = "1.17"

// Suppiled by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null               : "$System.env.JAVADOCS_DIR"

group = "com.meowj"
archivesBaseName = "LangUtils-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber"

sourceCompatibility = 1.8
targetCompatibility = 1.8

// extra compile warnings
compileJava {
    options.compilerArgs += ["-Xlint:deprecation,unchecked,rawtypes"]
    options.encoding = 'UTF-8'
}

// default repositories
repositories {
    jcenter()
    maven { name 'Spigot';      url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype';    url 'https://oss.sonatype.org/content/groups/public' }
}

// default dependencies
dependencies {
    compileOnly "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
}

// write javadocs to an external folder which can be served via nginx
if (ext.jdDirectory != null) {
    javadoc.destinationDir = file("${jdDirectory}/languageutils-${version}")
}

// Source code JAR
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

// Javadoc JAR
task javadocJar(type: Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

//noinspection GroovyAssignabilityCheck
processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

publishing {
    publications {
        defaultPublication(MavenPublication) {
            artifactId 'LangUtils'
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "$mavenDirectory"
        }
    }
}

// options for generating Javadoc
javadoc {
    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)
        addStringOption('Xdoclint:none', '-quiet')

        if (JavaVersion.current() > JavaVersion.VERSION_1_9) {
            options.addBooleanOption('html5', true)
        }

        windowTitle = "LangUtils Javadoc"
        docTitle = "LangUtils (mc$minecraftVersion-${project.version})"
    }
}
